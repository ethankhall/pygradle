apply plugin: 'maven-publish'
apply plugin: 'ivy-publish'
apply plugin: 'com.jfrog.artifactory'

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar
}

publishing {
    publications {
        standardjava(MavenPublication) {
            from components.java
            artifact sourceJar
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    description 'PyGradle, Building Python with Gradle'
                    url 'https://github.com/linkedin/pygradle'
                    scm {
                        url 'https://github.com/linkedin/pygradle'
                        connection 'scm:git:git://github.com/linkedin/pygradle.git'
                        developerConnection 'scm:git:ssh:git@github.com:linkedin/pygradle.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
        ivyJava(IvyPublication) {
            if (project.version.isSnapshot() && project.hasProperty('enableLongVersion')) {
                revision = project.version.withBuildNumber(startTime)
                println("Testing Version : " + revision)
            }
            from components.java
        }
    }
    repositories {
        ivy {
            url "${ System.getProperty("user.home") }/local-repo"
            layout('pattern') {
                ivy "[organisation]/[module]/[revision]/[module]-[revision].ivy"
                artifact "[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier]).[ext]"
                m2compatible true
            }
        }
    }
}

artifactoryPublish.dependsOn assemble
artifactoryPublish.dependsOn publishToMavenLocal
